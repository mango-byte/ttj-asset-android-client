apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    lintOptions {
        disable 'GradleCompatible'
    }
    def globalConfiguration = rootProject.ext

    compileSdkVersion globalConfiguration.androidCompileSdkVersion
    buildToolsVersion globalConfiguration.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion globalConfiguration.androidMinSdkVersion
        targetSdkVersion globalConfiguration.androidTargetSdkVersion
        versionCode globalConfiguration.dataLibraryVersionCode
        versionName globalConfiguration.dataLibraryVersionName
        testInstrumentationRunner globalConfiguration.testInstrumentationRunner
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../buildSystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {

    // Compile
    def compileTimeDependencies = rootProject.ext.compileTimeDependencies
    kapt compileTimeDependencies.daggerCompiler
    kapt compileTimeDependencies.daggerAndroidProcessor
    compileOnly compileTimeDependencies.javaxAnnotation
    compileOnly compileTimeDependencies.javaxInject
    kapt compileTimeDependencies.ormaProcessor

    // Common
    def commonDependencies = rootProject.ext.commonDependencies
    for (dependency in commonDependencies){
        implementation dependency.value
    }

    // Data
    def dataDependencies = rootProject.ext.dataDependencies
    for (dependency in dataDependencies){
        implementation dependency.value
    }

    // Test
    def testDependencies = rootProject.ext.testDependencies
    for (dependency in testDependencies){
        testImplementation dependency.value
    }

    //Module Dependencies
    implementation project(':domain')
}

